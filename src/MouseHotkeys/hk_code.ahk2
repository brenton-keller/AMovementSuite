; Shared volume notification function
ShowVolumeNotification(volumeLevel, isMuted := false) {
    static volGui := ""
    static textCtrl := ""
    static progressCtrl := ""
    static dismissTimer := ""
    static lastMuteState := false

    ; Build notification text
    if (isMuted) {
        mainText := "VOLUME: MUTED"
    } else {
        mainText := "VOLUME: " Round(volumeLevel) "%"
    }

    ; If GUI doesn't exist or mute state changed, recreate it
    if (!volGui || lastMuteState != isMuted) {
        ; Destroy old GUI if it exists
        if (volGui) {
            try volGui.Destroy()
        }

        ; Create notification GUI
        volGui := Gui("+ToolWindow -Caption +AlwaysOnTop +E0x20")
        volGui.BackColor := "1a1a1a"
        volGui.SetFont("s14 cWhite Bold", "Segoe UI")

        ; Add text control
        textCtrl := volGui.Add("Text", "x20 y15 w360 cWhite Center", mainText)

        ; Add progress bar for visual feedback (unless muted)
        if (!isMuted) {
            volGui.SetFont("s10 cWhite", "Segoe UI")
            progressCtrl := volGui.Add("Progress", "x20 y55 w360 h15 c00FF00 Background333333", Round(volumeLevel))
        }

        ; Position at center of screen
        guiHeight := isMuted ? "60" : "90"
        volGui.Show("x" (A_ScreenWidth/2 - 200) " y" (A_ScreenHeight/2 - 45) " w400 h" guiHeight " NoActivate")
        WinSetTransparent 230, volGui.Hwnd

        lastMuteState := isMuted
    }
    else {
        ; GUI exists - just update the controls
        textCtrl.Value := mainText
        if (!isMuted && progressCtrl) {
            progressCtrl.Value := Round(volumeLevel)
        }
    }

    ; Clear existing timer if any
    if (dismissTimer) {
        SetTimer dismissTimer, 0
    }

    ; Set new auto-dismiss timer
    dismissTimer := () => (volGui ? (volGui.Destroy(), volGui := "", textCtrl := "", progressCtrl := "") : "")
    SetTimer dismissTimer, -1000
}

;Send {7} - Nest code (RAlt+F7)
*F7::{
    if !GetKeyState("RAlt", "P") {
        Send "{Blind}{F7}"
        return
    }
    if GetKeyState("LCtrl", "P") && GetKeyState("LShift", "P") && GetKeyState("LAlt", "P") {
        Send("^k")
        Send("^j")
        Send("^a")
        Send("^k")
        Send("^3")
        Send("{Escape}")
    }
    else if GetKeyState("LCtrl", "P") && GetKeyState("LShift", "P") {
        Send("^k")
        Send("^j")
        Send("^a")
        Send("^k")
        Send("^2")
        Send("{Escape}")
    }
    else if GetKeyState("LCtrl", "P") {
        Send("^a")
        Send("^k")
        Send("^1")
        Send("{Escape}")
    }
    else {
        Send("^+{[}")
    }
}

;Send {10} - Unnest code / Mute Toggle (RAlt+F10)
*F10::{
    if !GetKeyState("RAlt", "P") {
        Send "{Blind}{F10}"
        return
    }
    if GetKeyState("LAlt", "P") {
        SoundSetMute(-1)
        isMuted := SoundGetMute()
        if (isMuted) {
            ShowVolumeNotification(0, true)
        } else {
            currentVol := SoundGetVolume()
            ShowVolumeNotification(currentVol, false)
        }
    }
    else if GetKeyState("LCtrl", "P") {
        Send("^k")
        Send("^j")
    }
    else {
        Send("^+{]}")
    }
}

;Send {11} - Comment/Uncomment / Volume Down (RAlt+F11)
*F11::{
    if !GetKeyState("RAlt", "P") {
        Send "{Blind}{F11}"
        return
    }
    if GetKeyState("LAlt", "P") {
        currentVol := SoundGetVolume()
        newVol := Max(0, currentVol - 1)
        SoundSetVolume(newVol)
        ShowVolumeNotification(newVol, false)
    }
    else {
        ActiveProcess := WinGetProcessName("A")
        switch ActiveProcess {
            case "cursor.exe", "Code.exe":
                Send("^/")
            default:
                Send("{11}")
        }
    }
}
